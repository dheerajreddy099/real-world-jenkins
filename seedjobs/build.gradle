apply plugin: 'groovy'
defaultTasks 'libs'

repositories {
  mavenCentral()
  jcenter()
  maven { url 'http://repo.jenkins-ci.org/releases/' }
}

sourceSets {
  jobs {
    groovy {
      srcDirs 'jobs'
      compileClasspath += main.compileClasspath
    }
    compileClasspath += sourceSets.main.output
    runtimeClasspath += sourceSets.main.output
  }
}

configurations {
  seedjobLib
  testPlugins
}

dependencies {
  seedjobLib 'org.kohsuke:github-api:1.70'
  compile 'org.jenkins-ci.plugins:job-dsl-core:1.51'

  testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
  testCompile 'cglib:cglib-nodep:2.2.2' // used by Spock

  testCompile 'org.jenkins-ci.plugins:job-dsl:1.51'

  // Jenkins test harness dependencies
  testCompile 'org.jenkins-ci.main:jenkins-test-harness:2.8'
  testCompile "org.jenkins-ci.main:jenkins-war:${jenkinsVersion}"
  testCompile "org.jenkins-ci.main:jenkins-war:${jenkinsVersion}:war-for-test@jar"

  // Job DSL plugin including plugin dependencies
  testCompile "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}"
  testCompile "org.jenkins-ci.plugins:job-dsl:${jobDslVersion}@jar"
  testCompile 'org.jenkins-ci.plugins:structs:1.1@jar'
  testCompile 'org.jenkins-ci.plugins:cloudbees-folder:4.4@jar'
}

task resolveTestPlugins(type: Copy) {
  from configurations.testPlugins
  into new File(sourceSets.test.output.resourcesDir, 'test-dependencies')
  include '*.hpi'
  include '*.jpi'

  doLast {
    def baseNames = source.collect { it.name[0..it.name.lastIndexOf('.')-1] }
    new File(destinationDir, 'index').setText(baseNames.join('\n'), 'UTF-8')
  }
}

test {
  dependsOn tasks.resolveTestPlugins
  inputs.files sourceSets.jobs.groovy.srcDirs

  // set build directory for Jenkins test harness, JENKINS-26331
  systemProperty 'buildDirectory', project.buildDir.absolutePath
}

task libs(type: Copy) {
  into 'lib'
  from configurations.seedjobLib
}

clean {
  delete 'lib'
}

build.dependsOn libs